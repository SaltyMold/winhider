; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "WinHider"
#define MyAppVersion "0.8.14"
#define MyAppPublisher "Bitmutex Technologies"
#define MyAppURL "https://github.com/aamitn/winhider"

[Code]
function MyAppExeName(Param: String): String;
begin
  if IsWin64 then 
    Result := 'Winhider.exe'
  else 
    Result := 'Winhider_32bit.exe';
end;
function MyAppExeGuiName(Param: String): String;
begin
  if IsWin64 then 
    Result := 'WinhiderGui.exe'
  else 
    Result := 'WinhiderGui_32bit.exe';
end;

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{4896775D-F364-4AF8-AD6C-946EE5F49D95}
;SignTool=winsdk_signtool
AppName={#MyAppName} 
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\{#MyAppName}
DisableProgramGroupPage=yes
; Uncomment the following line to run in non administrative install mode (install for current user only.)
;PrivilegesRequired=lowest
PrivilegesRequiredOverridesAllowed=dialog
OutputBaseFilename=WinhiderInstaller
Compression=lzma
SolidCompression=yes
WizardStyle=modern
; Custom install options
ArchitecturesInstallIn64BitMode=x64compatible 
SetupIconFile=whicon.ico

UninstallDisplayIcon={app}\{code:MyAppExeName}
UninstallDisplayName={#MyAppName}

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
; Add VC Redist 2015–2022 Installers [ https://aka.ms/vs/17/release/vc_redist.x86.exe and  https://aka.ms/vs/17/release/vc_redist.x64.exe ]
Source: ".\redistributables\vc_redist.x86.exe"; DestDir: {tmp}; Flags: deleteafterinstall  
Source: ".\redistributables\vc_redist.x64.exe"; DestDir: {tmp}; Flags: deleteafterinstall    

Source: "..\Build\bin\Release\*.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\Build\bin\Release\*.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\hide_hotkey.ahk"; DestDir: "{app}"; Flags: ignoreversion

[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{code:MyAppExeName}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{code:MyAppExeName}"; Tasks: desktopicon

Name: "{autoprograms}\{#MyAppName}GUI"; Filename: "{app}\{code:MyAppExeGuiName}"
Name: "{autodesktop}\{#MyAppName}GUI"; Filename: "{app}\{code:MyAppExeGuiName}"; Tasks: desktopicon

[Run]
; Install VC Redist 2015–2022 x86
Filename: "{tmp}\vc_redist.x86.exe"; \
    Parameters: "/passive /norestart"; \
    StatusMsg: "Installing VC++ 2015–2022 x86 Redistributable..."; \
    Flags: waituntilterminated

; Install VC Redist 2015–2022 x64
Filename: "{tmp}\vc_redist.x64.exe"; \
    Parameters: "/passive /norestart"; \
    StatusMsg: "Installing VC++ 2015–2022 x64 Redistributable..."; \
    Flags: waituntilterminated
    
Filename: "{app}\{code:MyAppExeGuiName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent; WorkingDir: "{app}"

[UninstallDelete]
Type: dirifempty; Name: "{app}"
