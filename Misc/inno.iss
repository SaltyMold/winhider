; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "WinHider"
#define MyAppVersion "0.8.15"
#define MyAppPublisher "Bitmutex Technologies"
#define MyAppURL "https://github.com/aamitn/winhider"

[Code]
//variable declarations
var
  VC_x86_Downloaded: Boolean;
  VC_x64_Downloaded: Boolean;
  downloadFailed: Boolean;
// CLI App Name  
function MyAppExeName(Param: String): String;
begin
  if IsWin64 then 
    Result := 'Winhider.exe'
  else 
    Result := 'Winhider_32bit.exe';
end;

// GUI App Name  
function MyAppExeGuiName(Param: String): String;
begin
  if IsWin64 then 
    Result := 'WinhiderGui.exe'
  else 
    Result := 'WinhiderGui_32bit.exe';
end;

// Log Download Progress
function OnDownloadProgress(const Url, Filename: String; const Progress, ProgressMax: Int64): Boolean;
begin
  if ProgressMax <> 0 then
    Log(Format('%s: %d of %d bytes done.', [Filename, Progress, ProgressMax]))
  else
    Log(Format('%s: %d bytes done.', [Filename, Progress]));
  Result := True;
end;

// Download the VC Redist files
function InitializeSetup: Boolean;
begin
  VC_x86_Downloaded := False;
  VC_x64_Downloaded := False;
  downloadFailed := False;

  try
    DownloadTemporaryFile(
      'https://aka.ms/vs/17/release/vc_redist.x86.exe',
      'vc_redist.x86.exe',
      '', @OnDownloadProgress
    );
    VC_x86_Downloaded := True;
  except
    Log('Failed to download vc_redist.x86.exe: ' + GetExceptionMessage);
    downloadFailed := True;
  end;

  if Is64BitInstallMode then
  begin
    try
      DownloadTemporaryFile(
        'https://aka.ms/vs/17/release/vc_redist.x64.exe',
        'vc_redist.x64.exe',
        '', @OnDownloadProgress
      );
      VC_x64_Downloaded := True;
    except
      Log('Failed to download vc_redist.x64.exe: ' + GetExceptionMessage);
      downloadFailed := True;
    end;
  end;

  if downloadFailed then
  begin
    MsgBox(
      'One or more required VC++ Redistributable files could not be downloaded.' + #13#10#13#10 +
      'Please download and install them manually from the following links:' + #13#10 +
      'x86: https://aka.ms/vs/17/release/vc_redist.x86.exe' + #13#10 +
      'x64: https://aka.ms/vs/17/release/vc_redist.x64.exe' + #13#10#13#10 +
      'Click OK to continue with the setup.',
      mbError, MB_OK
    );
  end;

  Result := True; // Let the setup continue
end;

// Set Flags for vc redist downloads
function ShouldRunVCX86: Boolean;
begin
  Result := VC_x86_Downloaded;
end;

function ShouldRunVCX64: Boolean;
begin
  Result := VC_x64_Downloaded and Is64BitInstallMode;
end;


[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{4896775D-F364-4AF8-AD6C-946EE5F49D95}
;SignTool=winsdk_signtool
AppName={#MyAppName} 
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\{#MyAppName}
DisableProgramGroupPage=yes
; Uncomment the following line to run in non administrative install mode (install for current user only.)
;PrivilegesRequired=lowest
PrivilegesRequiredOverridesAllowed=dialog
OutputBaseFilename=WinhiderInstaller
Compression=lzma
SolidCompression=yes
WizardStyle=modern
; Custom install options
ArchitecturesInstallIn64BitMode=x64compatible 
SetupIconFile=whicon.ico

UninstallDisplayIcon={app}\{code:MyAppExeName}
UninstallDisplayName={#MyAppName}

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Tasks]
Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
; Add VC Redist 2015–2022 Installers [ https://aka.ms/vs/17/release/vc_redist.x86.exe and  https://aka.ms/vs/17/release/vc_redist.x64.exe ]
; Uncomment below to use local files
; Source: ".\redistributables\vc_redist.x86.exe"; DestDir: {tmp}; Flags: deleteafterinstall  
; Source: ".\redistributables\vc_redist.x64.exe"; DestDir: {tmp}; Flags: deleteafterinstall    

Source: "..\Build\bin\Release\*.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\Build\bin\Release\*.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\hide_hotkey.ahk"; DestDir: "{app}"; Flags: ignoreversion

[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{code:MyAppExeName}"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{code:MyAppExeName}"; Tasks: desktopicon

Name: "{autoprograms}\{#MyAppName}GUI"; Filename: "{app}\{code:MyAppExeGuiName}"
Name: "{autodesktop}\{#MyAppName}GUI"; Filename: "{app}\{code:MyAppExeGuiName}"; Tasks: desktopicon

[Run]
; Install VC Redist 2015–2022 x86
Filename: "{tmp}\vc_redist.x86.exe"; \
    Parameters: "/passive /norestart"; \
    StatusMsg: "Installing VC++ x86 Redistributable..."; \
    Flags: waituntilterminated; \
    Check: ShouldRunVCX86

; Install VC Redist 2015–2022 x64
Filename: "{tmp}\vc_redist.x64.exe"; \
    Parameters: "/passive /norestart"; \
    StatusMsg: "Installing VC++ x64 Redistributable..."; \
    Flags: waituntilterminated; \
    Check: ShouldRunVCX64
    
Filename: "{app}\{code:MyAppExeGuiName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent; WorkingDir: "{app}"

[UninstallDelete]
Type: dirifempty; Name: "{app}"
